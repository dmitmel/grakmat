///////////////////////// PLUGINS /////////////////////////
buildscript {
    ext {
        kotlinVersion = '1.0.4'
        junitVersion = '4.12'
        dokkaVersion = '0.9.9'
    }
    
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
///////////////////////// END OF PLUGINS /////////////////////////

group = 'org.drimachine.grakmat'
version = '1.0'

dokka {
    sourceDirs = files("$projectDir/src/main/kotlin")
}

sourceSets {
    main { output.resourcesDir = output.classesDir }
    test { output.resourcesDir = output.classesDir }
}

///////////////////////// DEPENDENCIES /////////////////////////
repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "junit:junit:$junitVersion"
}
///////////////////////// END OF DEPENDENCIES /////////////////////////

///////////////////////// ARTIFACTS AND JARS /////////////////////////
task sourcesJar(type: Jar, group: 'build', description: 'Assembles a jar archive containing all sources.') {
    classifier = 'sources'
    from sourceSets['main'].allSource
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    // Adding license file
    from file("$projectDir/LICENSE")
}

artifacts {
    archives jar
    archives sourcesJar
}
///////////////////////// END OF ARTIFACTS AND JARS /////////////////////////

///////////////////////// PUBLISHING /////////////////////////
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            
            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
    
//    repositories {
//        maven {
//            url "http://repo1.maven.org/maven/"
//        }
//    }
}
///////////////////////// END OF PUBLISHING /////////////////////////
